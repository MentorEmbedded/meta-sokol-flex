# ---------------------------------------------------------------------------------------------------------------------
# SPDX-License-Identifier: MIT
# ---------------------------------------------------------------------------------------------------------------------

BBPATH .= ":${LAYERDIR}"
BBFILES += "\
    ${LAYERDIR}/recipes-*/*/*.bb \
    ${LAYERDIR}/recipes-*/*/*.bbappend \
"

DYNAMIC_DIRS_flex-staging = "${@' '.join(set(os.listdir('${LAYERDIR}/dynamic-layers')) & set(d.getVar('BBFILE_COLLECTIONS').split()))}"
BBFILES_DYNAMIC += "${@' '.join( \
    '%s:${LAYERDIR}/dynamic-layers/%s/recipes-*/*/*.bb %s:${LAYERDIR}/dynamic-layers/%s/recipes-*/*/*.bbappend' \
        % (layer, layer, layer, layer) \
    for layer in d.getVar('DYNAMIC_DIRS_flex-staging').split() \
)}"

LAYERDIR_flex-staging = "${LAYERDIR}"
BBFILE_COLLECTIONS += "flex-staging"
BBFILE_PRIORITY_flex-staging = "10"
BBFILE_PATTERN_flex-staging = "^${LAYERDIR_RE}/"
LAYERDEPENDS_flex-staging = "core flex-common"
LAYERSERIES_COMPAT_flex-staging = "scarthgap"

# We don't want systemd and everything depending on systemd to rebuild when
# the metadata stored in os-release changes. TODO: push this to oe-core
SIGGEN_EXCLUDERECIPES_ABISAFE:append:feature-flex-staging = " os-release"

INHERIT:append = " feature_overrides"
FEATUREOVERRIDES .= "${@bb.utils.contains('DISTRO_FEATURES', 'flex-staging', ':feature-flex-staging', '', d)}"
