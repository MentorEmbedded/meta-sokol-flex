# ---------------------------------------------------------------------------------------------------------------------
# SPDX-License-Identifier: MIT
# ---------------------------------------------------------------------------------------------------------------------

BBPATH .= ":${LAYERDIR}"
BBFILES += "\
    ${LAYERDIR}/recipes-*/*/*.bb \
    ${LAYERDIR}/recipes-*/*/*.bbappend \
"

DYNAMIC_DIRS_sokol-flex-staging = "${@' '.join(set(os.listdir('${LAYERDIR}/dynamic-layers')) & set(d.getVar('BBFILE_COLLECTIONS').split()))}"
BBFILES_DYNAMIC += "${@' '.join( \
    '%s:${LAYERDIR}/dynamic-layers/%s/recipes-*/*/*.bb %s:${LAYERDIR}/dynamic-layers/%s/recipes-*/*/*.bbappend' \
        % (layer, layer, layer, layer) \
    for layer in d.getVar('DYNAMIC_DIRS_sokol-flex-staging').split() \
)}"

LAYERDIR_sokol-flex-staging = "${LAYERDIR}"
BBFILE_COLLECTIONS += "sokol-flex-staging"
BBFILE_PRIORITY_sokol-flex-staging = "10"
BBFILE_PATTERN_sokol-flex-staging = "^${LAYERDIR_RE}/"
LAYERDEPENDS_sokol-flex-staging = "core sokol-flex-common"
LAYERSERIES_COMPAT_sokol-flex-staging = "mickledore"

# We don't want systemd and everything depending on systemd to rebuild when
# the metadata stored in os-release changes. TODO: push this to oe-core
SIGGEN_EXCLUDERECIPES_ABISAFE:append:feature-sokol-flex-staging = " os-release"

INHERIT:append = " feature_overrides"
FEATUREOVERRIDES .= "${@bb.utils.contains('DISTRO_FEATURES', 'sokol-flex-staging', ':feature-sokol-flex-staging', '', d)}"
